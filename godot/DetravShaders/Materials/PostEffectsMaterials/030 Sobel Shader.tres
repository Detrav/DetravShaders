[gd_resource type="Shader" format=2]

[resource]
code = "//https://docs.godotengine.org/ru/stable/tutorials/shaders/index.html
//https://docs.godotengine.org/ru/stable/tutorials/shaders/shader_reference/canvas_item_shader.html
shader_type canvas_item;

uniform sampler2D u_Texture;
uniform float u_Step;

float intensity(in vec4 color){
	return sqrt((color.x*color.x)+(color.y*color.y)+(color.z*color.z));
}

vec3 sobel(float stepx, float stepy, vec2 center){
	// get samples around pixel
    float tleft = intensity(texture(u_Texture,center + vec2(-stepx,stepy)));
    float left = intensity(texture(u_Texture,center + vec2(-stepx,0)));
    float bleft = intensity(texture(u_Texture,center + vec2(-stepx,-stepy)));
    float top = intensity(texture(u_Texture,center + vec2(0,stepy)));
    float bottom = intensity(texture(u_Texture,center + vec2(0,-stepy)));
    float tright = intensity(texture(u_Texture,center + vec2(stepx,stepy)));
    float right = intensity(texture(u_Texture,center + vec2(stepx,0)));
    float bright = intensity(texture(u_Texture,center + vec2(stepx,-stepy)));
 
	// Sobel masks (see http://en.wikipedia.org/wiki/Sobel_operator)
	//        1 0 -1     -1 -2 -1
	//    X = 2 0 -2  Y = 0  0  0
	//        1 0 -1      1  2  1
	
	// You could also use Scharr operator:
	//        3 0 -3        3 10   3
	//    X = 10 0 -10  Y = 0  0   0
	//        3 0 -3        -3 -10 -3
 
    float x = tleft + 2.0*left + bleft - tright - 2.0*right - bright;
    float y = -tleft - 2.0*top - tright + bleft + 2.0 * bottom + bright;
    float color = sqrt((x*x) + (y*y));
    return vec3(color,color,color);
 }

void fragment() {
	COLOR.xyz = sobel(u_Step,u_Step, UV);
}"
